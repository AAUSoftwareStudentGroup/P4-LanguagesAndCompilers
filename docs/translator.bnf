Translator -> Systems Rules eof

Systems -> System newline Systems
         | EPSILON

System -> goto Alias := Domain goto Domain
        | <=> Alias := Domain <=> Domain
        | </> Alias := Domain </> Domain

Domain -> ListDomain
        | TreeDomain

ListDomain -> [ Domains ]

Domains -> TreeDomain Domains
         | EPSILON

TreeDomain -> Symbol

Symbol -> symbol
        | escapedSymbol

Rules -> Rule RulesP
RulesP -> newline Rule RulesP
        | EPSILON

Rule -> Conclusion Premises
      | EPSILON

Conclusion -> Pattern NewlineGoto Alias Structure

NewlineGoto -> newline goto
             | goto

Premises -> indent Premis PremisesP dedent
          | EPSILON
PremisesP -> newline Premis PremisesP
           | EPSILON

Premis -> Structure StructureOperation
        | EPSILON

StructureOperation -> Goto
                    | Equal
                    | NotEqual

Goto -> goto Alias Pattern

Equal -> <=> Alias Structure

NotEqual -> </> Alias Structure

Pattern -> ListPattern
         | TreePattern

ListPattern -> [ Patterns ]

TreePattern -> Name Alias ChildrenPattern

Name -> Symbol

Alias -> : symbol
       | EPSILON

ChildrenPattern -> [ Patterns ]
                 | EPSILON

Patterns -> TreePattern Patterns
          | EPSILON

Structure -> ListStructure
		   | TreeStructure

ListStructure -> [ Structures ]

TreeStructure -> Placeholder Name ChildrenStructure Insertion

Placeholder -> %
	     | EPSILON

Insertion -> insert TreeStructure
           | EPSILON

ChildrenStructure -> [ Structures ]
                   | EPSILON

Structures -> TreeStructure Structures
            | EPSILON
