Converter -> Systems Rules eof

Systems -> Name : Domain goto Domain

Domain -> Name 
        | [ Names ]

Rules -> Rule RulesP
RulesP -> newline Rule RulesP
        | EPSILON

Rule -> Conclusion Premises
      | EPSILON

Conclusion -> Pattern NewlineGoto Structure

NewlineGoto -> newline goto : Name
             | goto : Name

Premises -> indent Premis PremisesP dedent
          | EPSILON
PremisesP -> newline Premis PremisesP
           | EPSILON

Premis -> Structure StructureOperation
        | EPSILON

StructureOperation -> Goto
					| Equal
					| NotEqual

Goto -> goto : Name Pattern

Equal -> <=> Structure

NotEqual -> </> Structure

Pattern -> ListPattern
         | TreePattern

ListPattern -> [ Patterns ]

TreePattern -> Name Alias ChildrenPattern

Name -> symbol

Names -> Name Names
       | EPSILON

Alias -> : symbol
       | EPSILON

ChildrenPattern -> [ Patterns ]
                 | EPSILON

Patterns -> TreePattern Patterns
          | EPSILON

Structure -> ListStructure
		   | TreeStructure

ListStructure -> [ Structures ]

TreeStructure -> Placeholder Name ChildrenStructure Insertion

Placeholder -> %
			 | EPSILON

Insertion -> insert TreeStructure
           | EPSILON

ChildrenStructure -> [ Structures ]
                   | EPSILON

Structures -> TreeStructure Structures
         | EPSILON
