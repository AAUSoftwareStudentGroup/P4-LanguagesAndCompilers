AST -> GlobalStatement eof
	 | eof

GlobalStatement -> Interrupt
                 | CompoundGlobalStatement
                 | Statement

Interrupt -> interrupt ( intLiteral ) indent Statement dedent

CompoundGlobalStatement -> GlobalStatement newline GlobalStatement

Statement -> EPSILON 
           | CompoundStatement
           | IntegerDeclaration
           | BooleanDeclaration
           | DirectBitAssignment
           | IndirectBitAssignment
           | RegisterDeclaration 
           | IntegerAssignment
           | BooleanAssignment
           | RegisterAssignment
           | IfStatement 
           | WhileStatement

CompoundStatement -> Statement newline Statement

IntegerDeclaration -> intType identifier
		   
IntegerAssignment -> identifier = IntegerExpression

BooleanDeclaration -> booleanType identifier

BooleanAssignment -> identifier = BooleanExpression

DirectBitAssignment -> registerType ( intLiteral ) { IntegerExpression } = BooleanExpression

IndirectBitAssignment -> identifier { IntegerExpression } = BooleanExpression

RegisterDeclaration -> registerType identifier

RegisterAssignment -> identifier = RegisterExpression

RegisterExpression -> RegisterLiteral
					| RegisterVariable

RegisterLiteral -> registerType ( intLiteral )

RegisterVariable -> identifier

IfStatement -> if ( BooleanExpression ) indent Statement dedent 
		   
WhileStatement -> while ( BooleanExpression ) indent Statement dedent
		   
IntegerExpression -> IntegerVariable   
                   | IntegerParenthesisExpression
                   | AddExpression
                   | SubExpression
                   | MulExpression
                   | DivExpression 
                   | ModExpression
                   | PowExpression
                   | intLiteral

IntegerVariable -> identifier

IntegerParenthesisExpression -> ( IntegerExpression )        

AddExpression -> IntegerExpression + IntegerExpression

SubExpression -> IntegerExpression - IntegerExpression

MulExpression -> IntegerExpression * IntegerExpression

DivExpression -> IntegerExpression / IntegerExpression

ModExpression -> IntegerExpression % IntegerExpression

PowExpression -> IntegerExpression ^ IntegerExpression

BooleanExpression -> true 
                   | false 
				   | BooleanVariable
                   | DirectBitValue
                   | IndirectBitValue
                   | BooleanParenthesisExpression
                   | IntegerEqExpression 
                   | BooleanEqExpression 
                   | BooleanNotEqExpression
                   | IntegerNotEqExpression
                   | LessThanExpression 
                   | GreaterThanExpression
                   | LessThanOrEqExpression
                   | GreaterThanOrEqExpression
                   | NotExpression 
                   | AndExpression 
                   | OrExpression

BooleanVariable -> identifier

DirectBitValue -> registerType ( intLiteral ) { IntegerExpression }

IndirectBitValue -> RegisterVariable { IntegerExpression }

BooleanParenthesisExpression -> ( BooleanExpression )

IntegerEqExpression -> IntegerExpression = IntegerExpression

BooleanEqExpression -> BooleanExpression = BooleanExpression

IntegerNotEqExpression -> IntegerExpression != IntegerExpression

BooleanNotEqExpression -> BooleanExpression != BooleanExpression

LessThanExpression -> IntegerExpression < IntegerExpression

GreaterThanExpression -> IntegerExpression > IntegerExpression

LessThanOrEqExpression -> IntegerExpression <= IntegerExpression

GreaterThanOrEqExpression -> IntegerExpression >= IntegerExpression

NotExpression -> ! BooleanExpression

AndExpression -> BooleanExpression and BooleanExpression

OrExpression -> BooleanExpression or BooleanExpression
