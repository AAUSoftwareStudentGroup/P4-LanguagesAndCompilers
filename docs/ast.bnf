AST -> GlobalStatement eof
	 | eof

GlobalStatement -> Interrupt
                 | CompoundGlobalStatement
                 | Statement

Interrupt -> interrupt ( numeral ) indent Statement dedent

CompoundGlobalStatement -> GlobalStatement newline GlobalStatement

Statement -> EPSILON 
           | CompoundStatement
           | IntegerDeclaration
           | IntegerDeclarationInit
           | BooleanDeclaration
           | BooleanDeclarationInit
           | DirectBitAssignment
           | IndirectBitAssignment
           | RegisterDeclaration 
           | RegisterDeclarationInit
           | IntegerAssignment
           | BooleanAssignment
           | RegisterAssignment
           | IfStatement 
           | WhileStatement
           | ForStatement

CompoundStatement -> Statement newline Statement

IntegerDeclaration -> IntType identifier

IntegerDeclarationInit -> IntType identifier = IntegerExpression
		   
IntegerAssignment -> IntegerVariable = IntegerExpression

BooleanDeclaration -> BooleanType identifier

BooleanDeclarationInit -> BooleanType identifier = BooleanExpression

BooleanType -> bool

BooleanAssignment -> BooleanVariable = BooleanExpression

DirectBitAssignment -> RegisterType ( numeral ) { IntegerExpression } = BooleanExpression

IndirectBitAssignment -> RegisterVariable { IntegerExpression } = BooleanExpression

RegisterDeclaration -> RegisterType identifier

RegisterDeclarationInit -> RegisterType identifier = RegisterExpression

RegisterAssignment -> RegisterVariable = RegisterExpression

RegisterExpression -> RegisterLiteral
					| RegisterVariable

RegisterLiteral -> RegisterType ( numeral )

RegisterVariable -> identifier

IfStatement -> if ( BooleanExpression ) indent Statement dedent 
		   
WhileStatement -> while ( BooleanExpression ) indent Statement dedent

ForStatement -> for ( IntType identifier from IntegerExpression to IntegerExpression ) indent Statement dedent

IntType -> int8
         | int16
         | int32
         | uint8
         | uint16
         | uint32

IntegerExpression -> IntegerVariable   
                   | IntegerParenthesisExpression
                   | AddExpression
                   | SubExpression
                   | MulExpression
                   | DivExpression 
                   | ModExpression
                   | PowExpression
                   | numeral

IntegerVariable -> identifier

IntegerParenthesisExpression -> ( IntegerExpression )        

AddExpression -> IntegerExpression + IntegerExpression

SubExpression -> IntegerExpression - IntegerExpression

MulExpression -> IntegerExpression * IntegerExpression

DivExpression -> IntegerExpression / IntegerExpression

ModExpression -> IntegerExpression % IntegerExpression

PowExpression -> IntegerExpression ^ IntegerExpression

BooleanExpression -> true 
                   | false 
				   | BooleanVariable
                   | DirectBitValue
                   | IndirectBitValue
                   | BooleanParenthesisExpression
                   | IntegerEqExpression 
                   | BooleanEqExpression 
                   | BooleanNotEqExpression
                   | IntegerNotEqExpression
                   | LessThanExpression 
                   | GreaterThanExpression
                   | LessThanOrEqExpression
                   | GreaterThanOrEqExpression
                   | NotExpression 
                   | AndExpression 
                   | OrExpression

BooleanVariable -> identifier

DirectBitValue -> RegisterType ( numeral ) { IntegerExpression }

RegisterType -> register8
              | register16

IndirectBitValue -> RegisterVariable { IntegerExpression }

BooleanParenthesisExpression -> ( BooleanExpression )

IntegerEqExpression -> IntegerExpression == IntegerExpression

BooleanEqExpression -> BooleanExpression == BooleanExpression

IntegerNotEqExpression -> IntegerExpression != IntegerExpression

BooleanNotEqExpression -> BooleanExpression != BooleanExpression

LessThanExpression -> IntegerExpression < IntegerExpression

GreaterThanExpression -> IntegerExpression > IntegerExpression

LessThanOrEqExpression -> IntegerExpression <= IntegerExpression

GreaterThanOrEqExpression -> IntegerExpression >= IntegerExpression

NotExpression -> ! BooleanExpression

AndExpression -> BooleanExpression and BooleanExpression

OrExpression -> BooleanExpression or BooleanExpression
