Program -> Statements

Statements  -> Statement StatementsP
StatementsP -> newline Statement StatementsP
            | EPSILON
Statement -> Declaration
          |  IdentifierStatement
          |  IfStatement
          |  WhileStatement
          |  Class
          |  newLine

Class -> class identifier TypedParameters ClassBlock

ClassBlock -> indent ClassStatements dedent

ClassStatements  -> ClassStatement ClassStatementsP            
                 |  EPSILON
ClassStatementsP -> newline ClassStatement ClassStatementsP
                 | EPSILON

ClassStatement -> Declaration
               |  newLine

Declaration -> Type identifier Definition

IdentifierStatement -> identifier LIdentifierOperation

LIdentifierOperation -> Assignment
                     |  LSelectorOperation

LSelectorOperation -> Call LReturnValueOperation
                   |  ClassAccessor LIdentifierOperation
                   |  ElementSelector LIdentifierOperation
                   |  BitSelector Assignment

LReturnValueOperation -> EPSILON
                     |  LSelectorOperation

ElementSelector -> startBracket Expression endBracket

BitSelector -> startCurly Expression endCurly
                    
ClassAccessor -> dot identifier

Assignment -> assign Expression

Call -> Parameters

IfStatement -> if Condition SimpleBlock ElseStatement

ElseStatement -> else ElseBlock
              |  EPSILON
              
ElseBlock -> IfStatement
          |  indent SimpleBlock dedent

WhileStatement -> while Condition SimpleBlock

Condition -> startDel Expression endDel

Definition -> FunctionDefinition
           |  Assignment
           |  EPSILON

FunctionDefinition -> TypedParameters SimpleBlock

SimpleBlock -> indent SimpleStatements dedent

TypedParameters  -> startDel TypedParametersF
TypedParametersF -> Type identifier TypedParametersP
                 |   endDel
TypedParametersP -> sep Type identifier TypedParametersP
                 | endDel

Type -> simpleType TypeParameters
     |  startDel Type endDel startDel TypeParametersF endDel

TypeParameters  -> startDel TypeParametersF endDel
                | EPSILON
TypeParametersF -> Type TypeParametersP 
                | EPSILON
TypeParametersP -> sep Type TypeParametersP
                | EPSILON

SimpleStatements  -> SimpleStatement SimpleStatementsP            
SimpleStatementsP -> newLine SimpleStatement SimpleStatementsP
                  | EPSILON

SimpleStatement -> SimpleDeclaration
                |  Assignment
                |  IfStatement
                |  WhileStatement
                |  ReturnStatement
                |  EPSILON

ReturnStatement -> return Expression

SimpleDeclaration -> Type identifier assign Expression

Expression -> OrOperation

OrOperation  -> AndOperation OrOperationP
OrOperationP -> or AndOperation OrOperationP
             | EPSILON

AndOperation  -> EqualityOperation AndOperationP
AndOperationP -> and EqualityOperation AndOperationP
              | EPSILON

EqualityOperation  -> RelationalOperation EqualityOperationP
EqualityOperationP -> equality RelationalOperation EqualityOperationP
                   | EPSILON
   
RelationalOperation  -> AddSubOperation RelationalOperationP
RelationalOperationP -> relational AddSubOperation RelationalOperationP
                     | EPSILON

AddSubOperation  -> MultDivOperation AddSubOperationP
AddSubOperationP -> addSub MultDivOperation AddSubOperationP
                 | EPSILON

MultDivOperation  -> PrimaryOperation MultDivOperationP
MultDivOperationP -> multDiv PrimaryOperation MultDivOperationP
                  | EPSILON

PrimaryOperation -> startDel Expression endDel
                 |  identifier RSelectorOperation
                 |  intLiteral
                 |  boolLiteral
                 |  strLiteral
                 |  charLiteral

RSelectorOperation -> Call RReturnValueOperation
                   |  ClassAccessor RSelectorOperation
                   |  ElementSelector RSelectorOperation
                   |  BitSelector

RReturnValueOperation -> EPSILON
                      |  RSelectorOperation

Parameters  -> startDel ParametersF endDel
ParametersF -> Expression ParametersP
            |  EPSILON
ParametersP -> sep Expression ParametersP
            | EPSILON