Program -> Statements eof

Statements  -> Statement StatementsP
StatementsP -> newline Statement StatementsP
            |  EPSILON

Statement -> Declaration
          |  IdentifierStatement
          |  IfStatement
          |  WhileStatement
          |  ForStatement
          |  Interrupt
          |  Class
          |  EPSILON

Interrupt -> interrupt startDel Expression endDel SimpleBlock

Class -> class identifier ClassBlock

ClassBlock -> indent ClassStatements dedent

ClassStatements  -> ClassStatement ClassStatementsP            
                 |  EPSILON
ClassStatementsP -> newline ClassStatement ClassStatementsP
                 |  EPSILON

ClassStatement -> Declaration
               |  newline

Declaration -> Type identifier Definition

IdentifierStatement -> identifier LIdentifierOperation

LIdentifierOperation -> Assignment
                     |  LSelectorOperation

LSelectorOperation -> Call LReturnValueOperation
                   |  ClassAccessor LIdentifierOperation
                   |  ElementSelector LIdentifierOperation
                   |  BitSelector Assignment

LReturnValueOperation -> EPSILON
                      |  LSelectorOperation

ElementSelector -> startBracket Expression endBracket

BitSelector -> startCurly Expression endCurly
                    
ClassAccessor -> dot identifier

Assignment -> assign Expression

Call -> Parameters

IfStatement -> if Condition SimpleBlock ElseStatement

ElseStatement -> else ElseBlock
              |  EPSILON
              
ElseBlock -> IfStatement
          |  SimpleBlock

WhileStatement -> while Condition SimpleBlock

ForStatement -> for startDel simpleType identifier from Expression to Expression endDel SimpleBlock

Condition -> startDel Expression endDel

Definition -> FunctionDefinition
           |  Assignment
           |  EPSILON

FunctionDefinition -> TypedParameters SimpleBlock

SimpleBlock -> indent SimpleStatements dedent

TypedParameters  -> startDel TypedParametersF endDel
TypedParametersF -> Type identifier TypedParametersP
                 |  EPSILON
TypedParametersP -> sep Type identifier TypedParametersP
                 |  EPSILON

Type -> simpleType TypeParameters ArrayType
     |  startDel Type endDel startDel TypeParametersF endDel ArrayType

ArrayType -> startBracket ArraySize endBracket
          |  EPSILON

ArraySize -> Expression
          |  EPSILON

TypeParameters  -> startDel TypeParametersF endDel
                |  EPSILON
TypeParametersF -> Type TypeParametersP 
                |  EPSILON
TypeParametersP -> sep Type TypeParametersP
                |  EPSILON

SimpleStatements  -> SimpleStatement SimpleStatementsP            
SimpleStatementsP -> newline SimpleStatement SimpleStatementsP
                  |  EPSILON

SimpleStatement -> SimpleDeclaration
                |  Assignment
                |  IfStatement
                |  WhileStatement
                |  ReturnStatement
                |  EPSILON

ReturnStatement -> return Expression

SimpleDeclaration -> Type identifier SimpleDefinition

SimpleDefinition -> assign Expression
                 |  EPSILON

Expression -> OrOperation

OrOperation  -> AndOperation OrOperationP
OrOperationP -> or AndOperation OrOperationP
             |  EPSILON

AndOperation  -> EqualityOperation AndOperationP
AndOperationP -> and EqualityOperation AndOperationP
              |  EPSILON

EqualityOperation  -> RelationalOperation EqualityOperationP
EqualityOperationP -> equality RelationalOperation EqualityOperationP
                   |  EPSILON
   
RelationalOperation  -> AddSubOperation RelationalOperationP
RelationalOperationP -> relational AddSubOperation RelationalOperationP
                     |  EPSILON

AddSubOperation  -> MultDivOperation AddSubOperationP
AddSubOperationP -> addSub MultDivOperation AddSubOperationP
                 |  EPSILON

MultDivOperation  -> PrimaryOperation MultDivOperationP
MultDivOperationP -> multDiv PrimaryOperation MultDivOperationP
                  |  EPSILON

PrimaryOperation -> startDel Expression endDel
                 |  identifier RSelectorOperation
                 |  intLiteral
                 |  boolLiteral
                 |  Register OptionalBitSelector
                 |  Array OptionalElementSelector
                 
Register -> register startDel Expression endDel

OptionalBitSelector -> BitSelector
                    |  EPSILON

Array  -> startBracket ArrayF endBracket
ArrayF -> Expression ArrayP
       |  EPSILON
ArrayP -> sep Expression ArrayP
       |  EPSILON

OptionalElementSelector -> ElementSelector RSelectorOperation
                        |  EPSILON

RSelectorOperation -> Call RSelectorOperation
                   |  ClassAccessor RSelectorOperation
                   |  ElementSelector RSelectorOperation
                   |  BitSelector
                   |  EPSILON

Parameters  -> startDel ParametersF endDel
ParametersF -> Expression ParametersP
            |  EPSILON
ParametersP -> sep Expression ParametersP
            |  EPSILON